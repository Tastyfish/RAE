room park the "public park" {
	aka "park";
	
	on enter {
		// clear out gangster
		gangster goto policeStation;
		gangster to default;
	}
	
	spot north the "tennis court" {
		on enter {
			"You approach "+this+",";
			"it being the only thing really of interest in this "+Bold(park.name)+".";
			int event = roll(3);
			wait(1000);
			"There isn't much of interest...";
			game.tick();
			pause();
			game.tick();
			game.tick();
			int stealAmt;
			if event != 0 {
				"You find $1 lying on the ground!";
				wallet.addCash(1);
			} else if gangster isnt dead {
				"Suddenly, some gangsters surround you.";
				gangster: "What do ya got in yer pocket?";
				
				if park has patrolman1 {
					patrolman1: colorize("Halt, gangster scum!", "yellow");
					patrolman1.toTheString()+" shoots the gangster!";
					gangster do shoot;
					return;
				}
				
				if (has gun) && gun.ammoInClip > 0 {
					player: gun+"!";
					Verbable g = gun;
					gangster goto park;
					if !game.TryVerb(g, "use", new string[] {"use","gun","gangster"}) {
						"They leave in disappointment.";
					}
				} else {
					player: roll(2) == 0 ? "Gbuh..." : "Svrmm...";
					if wallet.cash() == 0 {
						stealAmt = proofs.amount() / 4;
						if stealAmt == 0 {
							"Good thing you had jack to steal!";
						} else {
							"They take " + stealAmt + " "+Bold("proofs of purchase")+" from you!";
							proofs.addAmount(-stealAmt);
						}
					} else {
						stealAmt = roll(wallet.cash());
						"They steal $" + stealAmt + " from you!";
						wallet.addCash(-stealAmt);
					}
				}
			} else {
				"You find "+Bold("a leaf")+", but don't bother picking it up.";
			}
		}
		on examine:
			"It's an average public tennis court in a chain-link fence.";
	}
	spot south the "main street in town" {
		on enter:
			goto street;
		on look {
			describe();
			"It's paved.";
		}
		on examine:
			"Luckilly, it's not very busy today.";
	}
	spot bushes some "bushes" {
		aka {"bush", "plants"};
		on examine:
			"They look green & healthy.";
		on pickup:
			"They're somewhat rooted to the ground.";
		on open {
			"You push them aside to find...";
			wait(500);
			if roll(100) == 0 {
				"$100!";
				wallet.addCash(100);
			} else {
				"nothing.";
			}
		}
	}
}

npc gangster a "gangster" {
	location = policeStation;
	
	state dead {
		name = colorize("dead gangster", "red");
		aka {"gangster", "mugger", "robber"};
		
		on look:
			"He looks like a bloodied corpse.";
		
		on examine:
			"Ew.";
	}
	
	on shoot {
		this: "I been shot!";
		"They scurry away.";
		this+" is now dead.";
		to dead;
	}
}
